buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath "com.github.dkorotych.gradle.maven.exec:gradle-maven-exec-plugin:${version}"
    }
}

apply plugin: "com.github.dkorotych.gradle-maven-exec"

def groupId = 'com.mycompany.app'
def artifactId = 'my-app'

task generateMavenApplication(type: MavenExec) {
    buildDir.mkdirs()
    ext.outputDir = file("$buildDir/$artifactId")
    outputs.dir outputDir
    workingDir buildDir
    goals 'archetype:generate'
    options {
        define = [
                'groupId'            : groupId,
                'artifactId'         : artifactId,
                'archetypeArtifactId': 'maven-archetype-quickstart',
                'interactiveMode'    : 'false'
        ]
    }
}

task packageMavenApplication(type: MavenExec, dependsOn: 'generateMavenApplication') {
    outputs.dir file("${generateMavenApplication.outputDir}/target")
    workingDir generateMavenApplication.outputDir
    goals 'package'
}

task checkMavenApplication(type: JavaExec, dependsOn: 'packageMavenApplication') {
    def outputFile = file("$buildDir/output.txt")
    doFirst {
        workingDir generateMavenApplication.outputDir
        classpath file("${generateMavenApplication.outputDir}/target/${artifactId}-1.0-SNAPSHOT.jar")
        main "${groupId}.App"
        standardOutput new FileOutputStream(outputFile)
    }
    doLast {
        assert outputFile.text.trim() == 'Hello World!'
    }
}

task checkMavenApplication2 {
    doLast {
        buildDir.mkdirs()
        def application = artifactId + '-second'
        def applicationDir = file("$buildDir/$application")
        delete applicationDir
        def outputFile = file("$buildDir/output.txt")
        mavenexec {
            workingDir buildDir
            goals 'archetype:generate'
            define([
                    'groupId'            : groupId,
                    'artifactId'         : application,
                    'archetypeArtifactId': 'maven-archetype-quickstart',
                    'interactiveMode'    : 'false'
            ])
            quiet true
        }
        mavenexec {
            workingDir applicationDir
            goals 'package', 'site'
            threads '2C'
            quiet true
        }
        javaexec {
            workingDir applicationDir
            classpath file("$applicationDir/target/$application-1.0-SNAPSHOT.jar")
            main "${groupId}.App"
            standardOutput new FileOutputStream(outputFile)
        }
        assert outputFile.text.trim() == 'Hello World!'
    }
}