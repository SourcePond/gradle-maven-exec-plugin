buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.6"
        if (org.gradle.api.JavaVersion.current().isJava8Compatible()) {
            classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.2.1"
            classpath 'ru.vyarus:gradle-quality-plugin:2.0.0'
        }
    }
}

plugins {
    id 'net.saliman.cobertura' version '2.3.2'
    id 'net.researchgate.release' version '2.4.1'
    id "nebula.info" version "3.3.4"
}

group 'com.github.dkorotych.gradle.maven.exec'

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: "com.gradle.plugin-publish"
apply plugin: 'idea'
if (buildscript.configurations.classpath.find { it.name =~ /^sonarqube-gradle-plugin.+$/ } != null) {
    apply plugin: "org.sonarqube"
    apply plugin: 'ru.vyarus.quality'

    quality {
        configDir = rootProject.file('config')
    }

    afterEvaluate {
        codenarc {
            configFile = rootProject.file('config/codenarc/codenarc.groovy')
        }
    }
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile "commons-lang:commons-lang:2.6"
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4', {
        exclude module: 'groovy-all'
    }
}

cobertura {
    coverageFormats = ['html', 'xml']
    coverageSourceDirs = sourceSets.main.allSource.srcDirs
}

pluginBundle {
    website = 'https://github.com/dkorotych/gradle-maven-exec-plugin'
    vcsUrl = 'https://github.com/dkorotych/gradle-maven-exec-plugin'
    description = 'Gradle plugin which provides an Maven exec task'
    tags = ['maven', 'exec', 'cross-platform']

    plugins {
        mavenExecPlugin {
            id = 'com.github.dkorotych.gradle-maven-exec'
            displayName = 'Gradle Maven Exec Plugin'
        }
    }
}

groovydoc {
    link 'http://docs.oracle.com/javase/7/docs/api/', 'java.', 'org.xml.', 'javax.', 'org.xml.'
    link "https://docs.gradle.org/${gradle.gradleVersion}/javadoc/", 'org.gradle'
    link "http://docs.groovy-lang.org/${GroovySystem.version}/html/gapi/",
            'groovy', 'org.apache.commons.cli', 'org.codehaus.groovy'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}

idea {
    project {
        vcs = 'Git'
        module {
            downloadJavadoc = true
            downloadSources = true
        }
        ipr {
            withXml {
                def node = it.asNode()

                def vcsConfig = node.component.find { it.'@name' == 'VcsDirectoryMappings' }
                vcsConfig.mapping[0].'@vcs' = 'Git'
            }
        }
    }
}

cleanIdea.doLast {
    delete file('.idea')
}

updateVersion.doFirst {
    def file = file('README.md')
    def content = file.text
    def versionPattern = /\d+(?:\.\d+)+/
    content = content.replaceAll("id \"com.github.dkorotych.gradle-maven-exec\" version \"${versionPattern}\"",
            "id \"com.github.dkorotych.gradle-maven-exec\" version \"${version}\"")
    content = content.replaceAll("gradle-maven-exec-plugin:${versionPattern}",
            "gradle-maven-exec-plugin:${version}")
    file.text = content
}

task realUseCaseTest(type: GradleBuild) {
    dependsOn 'install'
    group = 'verification'
    buildFile = file('useCase.gradle')
    tasks = ['checkMavenApplication', 'checkMavenApplication2']
}
